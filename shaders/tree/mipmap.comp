#version 440 core

#include "common/extensions.glsl"
#include "common/bindings.glsl"
#include "common/voxel.glsl"

layout (local_size_variable) in;

uniform uint u_nodeOffset; // 1
uniform uint u_nodesThisLevel; // 8

void main()
{
    // retrieve current thread id and return if out of bounds
    uint threadId = gl_GlobalInvocationID.x; // [0,64]
    if (threadId >= u_nodesThisLevel)
        return; // [0,7]

    uint parent = u_nodeOffset + threadId; // [1;8]
    uint child = octree[parent].id & 0x7FFFFFFF; // [9;17;...;65]
    vec4 colorSum = vec4(0.0);

    // check each child
    for (uint i = 0; i < 8; ++i) {

        vec4 col = octreeColor[child + i].color;
        if (col.w > 1.0) {
            // normalize
            col /= col.w;
        }
        if (col.w > 0.0) {
            // has color
            colorSum += col;
        }

    }

    if (colorSum.w != 0) {

        // write average into parent
        colorSum /= colorSum.w;
        atomicAdd(octreeColor[parent].color.x, colorSum.x);
        atomicAdd(octreeColor[parent].color.y, colorSum.y);
        atomicAdd(octreeColor[parent].color.z, colorSum.z);
        atomicAdd(octreeColor[parent].color.w, colorSum.w);

    }

}
